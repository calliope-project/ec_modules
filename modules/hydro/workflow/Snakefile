import glob
from pathlib import Path

from snakemake.utils import validate, min_version, makedirs

min_version("8.10")

configfile: "./config/default.yaml"

wildcard_constraints:
    resolution = "[a-zA-Z]+"

rule shapes:
    message: "Download spatial zones for {wildcards.resolution}."
    params:
        url = config["resolution-file"],
    output: "results/{resolution}/shapes.geojson"
    conda: "./envs/shell.yaml"
    shell: "curl -sSLo {output} '{params.url}'"

include: "./rules/hydro.smk"
include: "./rules/basins.smk"

rule all:
    localrule: True
    default_target: True
    input:
        expand("results/{resolution}/{first_year}-{final_year}/capacity-factors-run-of-river.csv",
            zip,
            resolution=config["resolution-name"],
            first_year=config["first-year"],
            final_year=config["final-year"]),
        expand("results/{resolution}/{first_year}-{final_year}/capacity-factors-reservoir.csv",
            zip,
            resolution=config["resolution-name"],
            first_year=config["first-year"],
            final_year=config["final-year"]),
        expand("results/{resolution}/supply-hydro.csv", resolution=config["resolution-name"]),
        expand("results/{resolution}/storage-hydro.csv", resolution=config["resolution-name"]),
