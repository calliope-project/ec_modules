import glob
from pathlib import Path

import yaml

from snakemake.utils import min_version

min_version("8.10")

configfile: "./config/default.yaml"
with open(workflow.source_path("resources/internal.yaml"), "r") as f:
    internal = yaml.safe_load(f)

wildcard_constraints:
    resolution = "[a-zA-Z]+"

rule download_shapes:
    message: "Download spatial zones for {wildcards.resolution}."
    params:
        url = config["resolution_file"],
    output:
        shapefile = "results/{resolution}/shapes.geojson"
    conda: "./envs/shell.yaml"
    shell: "curl -sSLo {output} '{params.url}'"

include: "./rules/hydro.smk"
include: "./rules/basins.smk"

rule all:
    localrule: True
    default_target: True
    input:
        expand("results/{resolution}/{first_year}-{final_year}/capacity-factors-run-of-river.csv",
            zip,
            resolution=config["resolution_name"],
            first_year=config["first_year"],
            final_year=config["final_year"]),
        expand("results/{resolution}/{first_year}-{final_year}/capacity-factors-reservoir.csv",
            zip,
            resolution=config["resolution_name"],
            first_year=config["first_year"],
            final_year=config["final_year"]),
        expand("results/{resolution}/supply-hydro.csv", resolution=config["resolution_name"]),
        expand("results/{resolution}/storage-hydro.csv", resolution=config["resolution_name"]),
