import yaml

from snakemake.utils import min_version, validate

# Limit the snakemake version to a modern one.
min_version("8.10")

# Load the default configuration. This will be overridden by users.
configfile: "config/default.yaml"
# Validate the configuration using the schema file.
validate(config, workflow.source_path("schemas/config.schema.yaml"))

# Load internal settings separately so users cannot modify them.
with open(workflow.source_path("internal/internal_config.yaml"), "r") as f:
    internal = yaml.safe_load(f)

# Add all your includes here.
include: "rules/downloads.smk"
include: "rules/basins.smk"
include: "rules/hydro.smk"

if config["use_default_user_resources"]:
    rule all:
        message: "Generate default output for 'hydropower'."
        input:
            expand("results/shapes/{resolution}/{year}/capacity_factors_ror.csv", resolution=["national"], year=[2016]),
            expand("results/shapes/{resolution}/{year}/capacity_factors_reservoir.csv", resolution=["national"], year=[2016])
